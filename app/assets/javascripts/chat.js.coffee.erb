set_online = (id) ->
  $('div.sidebar-nav li#' + id).addClass('online')

set_all_offline = (id) ->
  $('div.sidebar-nav li').removeClass('online')

scroll_to_bottom = ->
  $('.chatbox').stop().animate {
    scrollTop: $('.chatbox')[0].scrollHeight
  }, 800

compare_text = (a, b) ->
  $(a).text().toUpperCase().localeCompare($(b).text().toUpperCase())

compare_status = (a, b) ->
  if (a.className == "online")
    if (b.className == "online")
      return 0
    else
      return -1
  else if (b.className == "online")
    return 1

sort_members = ->
  list = $('div.sidebar-nav ul')

  items = list.children('li').get()
  items.sort (a, b) ->
    if a.className != b.className
      compare_status(a, b)
    else
      compare_text(a, b)

  $.each(items, (idx, itm) -> list.append(itm))

add_message = (data) ->
  # $('.chatbox .panel-body:first-child').remove()

  $('.chatbox').append '<div class="panel-body"><div class="interest_chat_box">' +
    '<a href="/users/' + data.user + '"><div class="thumbnail"></div></a>' +
      '<div class="message"><h5><a href="/users/' + data.user + '">' + data.username + '</a>' +
       '<span class="timestamp">' + data.timestamp + '</span></h5><p>' +
        data.content + '</div></div></div>'

  scroll_to_bottom()

add_member = (data) ->
  $('div.sidebar-nav ul').append('<li id="' + data.user + '"><a href="/users/' + data.user + '">' + data.username + '</a></li>')

remove_member = (data) ->
  $('div.sidebar-nav ul li#' + data.user).remove()

ready = ->
  if not $('#hashtag-id').length
    return

  scroll_to_bottom()

  scheme = <%= ENV['RAILS_ENV'] == "production" ? '"wss://"' : '"ws://"' %>
  host = <%= ENV['RAILS_ENV'] == "test" ? '"echo.websocket.org/"' : 'window.document.location.host' %>
  uri = scheme + host
  ws = new WebSocket(uri)
  
  hashtag = $('#hashtag-id')[0].value
  user = $('#user-id')[0].value

  ws.onopen = ->
    ws.send JSON.stringify
      event: 'online'
      hashtag: hashtag
      user: user

  ws.onmessage = (message) ->
    data = JSON.parse message.data
    console.log(data)

    if data.event == 'message'
      add_message data
    else if data.event == 'online'
      set_all_offline()
      set_online id for id in data.onlines
      sort_members()
    else if data.event == 'join'
      add_member data
      sort_members()
    else if data.event == 'leave'
      remove_member data

  $('#chat-send').on 'click', (event) ->
    event.preventDefault()
    
    text = $('#input-text')[0].value

    ws.send JSON.stringify
      event: 'message'
      content: text
      hashtag: hashtag
      user: user

    $('#input-text')[0].value = ''

$(document).ready(ready)
$(document).on('page:load', ready)
